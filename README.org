#+TITLE: Emacs Speaks NONMEM
#+AUTHOR: Matthew L. Fidler
* Introduction
Emacs Speaks NONMEM(R) is an Emacs extension that makes editing NONMEM
control streams easier.  Among other things, Emacs Speak NONMEM supports:

 - [[id:451f1a0f-4de1-412a-befa-84bdd4c184d9][Code Highlighting]]
 - Automatic Capitalization
 - Count number of input items
 - Count number of output items & Spit into multiple files if necessary
 - Display of 80 characters on line, and also automatic alignment and "wrapping"
   of records
 - Extended Control Stream Support
 - Tab Completion
 - Updating table names on copy or save-as
 - Ensuring a different purpose for each copy of the file.

Emacs Speaks NONMEM also integrates with the following programs:

 - Census
 - NMQual (submitting jobs only)
 - PLT Tools
 - Perl Speaks NONMEM (psn)
 - Pirana
 - Wings for NONMEM
 - Xpose

* Code Highlighting
:PROPERTIES:
:ID: 451f1a0f-4de1-412a-befa-84bdd4c184d9
:END:
Code highlighting is a standard feature of many development
platforms.  Highlighting implements context sensitive highlighting, and extended
variable highlighting.
** Version Based Highlighting
Now Emacs Speaks NONMEM does code highlighting based on the version of NONMEM
that the author is assumed to be using.  
** Adaptive Font Lock
With Emacs Speaks NONMEM, code highlighting may be
implemented with context sensitivity or context insensitivity.

For most editors highlighting statements like =ADVAN= would work in
both the =$INPUT= and =$SUBROUTINES= block.  However, with adaptive
font lock on, this is highlighted only in the appropriate record (=$SUB=)

#+BEGIN_SRC esn
  $INPUT ADVAN=
  $SUBROUTINES ADVAN=
#+END_SRC





** Highlighting unknown records
When enabled, EsN highlights unknown records (based on NONMEM version selected).
** Highlighting known options
When enabled, EsN highlights known option values (Based on NONMEM version selected)

** Highlight Input items over 4 characters
When using NONMEM 6 and below, highlight input items over 4 characters in
length.  This may be toggled.

** Highlight Input Items Over The Number of Data Items allowed
When using CSV files, highlight $INPUT data items that are OVER the number of
data items required.  This may be toggled.
** Highlight Operators 
When the option "highlighting operators" is enabled, operators and functions
such as =DEXP= are highlighted appropriately:
** Highlight Known/Reserved Variables on Left Handed Side of equations
This highlights known or reserved variables on the left handed side of equations
(Like =DADT(#)= in =$DES=)
** Highlight Known/Reserved Variables on Right Handed Side of Equations
Highlight known/reserved variables (like =THETA(1)=) on the right-handed side of
an equation in abbreviated code (like =$PK=).
** Highlight variables that should not be assigned
This Highlights variables that should not be assigned (Like =THETA(1)=) in =$PK=.
** Highlight Forbidden Variables
Highlight forbidden variables in abbreviated code.  For example, the =DADT(#)= is
forbidden in the =$PK= block.
** Highlight Mu_# not corresponding to ETA(#)
Highlights MU_# variables that do not match the eta(#) when =MU_#+ETA(#)= is specified.

For example, when enabled, the =MU_3= would be highlighted as a "bad" mu in the
following code:
#+BEGIN_SRC esn
  $PK
    MU_1 = DLOG(THETA(1))
    TVCL = EXP(MU_1)
    CL   = DEXP(MU_1+ETA(1))
    
    MU_3 = DLOG(THETA(2))   ; Should be MU_2
    TVV  = EXP(MU_3)
    V    = DEXP(MU_3+ETA(2))
    
    TVQ  = THETA(3)
    Q    = TVQ*DEXP(ETA(3))
    MU_5 = DLOG(TVQ) ; Should me MU_3
    
#+END_SRC


** Highlight Undefined variables in abbreviated code
When toggled, this will highlight variables that are undefined in the
control stream.  This option may be annoying to some people since
until you type the correct variable, the typing is red.  For example:

#+BEGIN_SRC esn
       $PK
         TVCL = THETA(1)
         CL   = TVC 
#+END_SRC
would be abbreviated in red while typing, until the full =TVCL= is specified as in:

#+BEGIN_SRC esn
       $pk
         TVCL = THETA(1)
         CL   = TVCL
#+END_SRC

** Highlighting known commons variables
With this enabled, EsN highlights known common variables like =THETA= when they
occur in the appropriate abbreviated record.  For example:
#+BEGIN_SRC esn
  $INFN 
        IF (ICALL.EQ.3) THEN 
        OPEN(50,FILE= 'cwtab1.est') 
        WRITE(50,*) 'ETAS' 
        DO WHILE(DATA) 
        IF (NEWIND.LE.1) WRITE (50,*) ETA 
        ENDDO 
        WRITE(50,*) 'THETAS' 
        WRITE(50,*) THETA 
        WRITE(50,*) 'OMEGAS' 
        WRITE(50,*) OMEGA(BLOCK) 
        WRITE(50,*) 'SIGMAS' 
        WRITE(50,*) SIGMA(BLOCK)
        ENDIF 
#+END_SRC
With it disabled, =THETA= is not highlighted:

** Highlight Extended Control Stream Variables
When enabled, EsN highlights extended control stream variables when the
occur in a control stream
* Changes While Editing
** Automatic Capitalization
While editing a control stream, Emacs Speaks NONMEM figures out what type of
capitalization needs to be present. Capitalization occurs outside of comments,
format specifiers, ignore characters, and file names to conform with NONMEM's
needs. In case your implementation of NONMEM doesn't require capitalization
(like wings for NONMEM), you can turn this off at:

 - Automation
   - Changes While Editing
     - Smart Capitalization

** Count Number of Input and Output Items
NONMEM can have 20/50 input items, and 20/50 PRED generated output
items.  Emacs Speaks NONMEM can automatically count the number of
INPUT items read in while typing.  A typical example is (under NONMEM
7 which supports 50 input items):

#+BEGIN_SRC esn
;| Variables 57/50 |;
$INPUT ID DV TIME CMT MDV EVID II SS V0 V1 V2 V3 V4 V5 V6 V7 V8 V9 V10 V11 V12
       V13 V14 V15 V16 V17 V18 V19 V20 V21 V22 V23 V24 V25 V26 V27 V28 V29 V30
       V31 V32 V33 V34 V35 V36 V37 V38 V39 V40 V41 V42 V43 V44 V45 V46 V47 V48
#+END_SRC

If seven of these variables are dropped or skipped, this input would be
sufficient for NONMEM 7.1.0:

#+BEGIN_SRC esn
;| Variables 50/50 |;
$INPUT ID DV TIME CMT MDV EVID II SS V0 V1 V2 V3 V4 V5 V6 V7 V8 V9 V10 V11 V12
       V13=DROP V14=SKIP V15=DROP V16=SKIP V17=DROP V18=SKIP V19=DROP V20 V21
       V22 V23 V24 V25 V26 V27 V28 V29 V30 V31 V32 V33 V34 V35 V36 V37 V38 V39
       V40 V41 V42 V43 V44 V45 V46 V47 V48
#+END_SRC

A new feature of EsN 0.13 is the ability to 

This automatic count can be toggled at:

 - Automation
   - Changes While Editing
     - Update $INPUT's number of variables


This is also performed on the number of variables output to a table:

For example:

#+BEGIN_SRC esn
;| Variables 50/50 |;
$INPUT ID DV TIME CMT MDV EVID II SS V0 V1 V2 V3 V4 V5 V6 V7 V8 V9 V10 V11 V12
       V13=DROP V14=SKIP V15=DROP V16=SKIP V17=DROP V18=SKIP V19=DROP V20 V21
       V22 V23 V24 V25 V26 V27 V28 V29 V30 V31 V32 V33 V34 V35 V36 V37 V38 V39
       V40 V41 V42 V43 V44 V45 V46 V47 V48

;; Other code skipped

;| Pred Variables 7/50 |;
$TABLE ID NUM EVID MDV V2 CL TVCL V TVV Q TVQ FILE=test.tbl
#+END_SRC

Notice that the variables in the =$INPUT= record do not count to the total number
of variables (since they are not generated by PRED).

This automatic counting may be toggled at:

 - Automation
   - Changes While Editing
     - Update $TABLE's number of PRED variables


** Tab Completion
Another feature that may be useful is Tab-completion. Tab completion tries to
guess what you are going to be typing based on the position you are in your
control stream.  There is general tab completion, and tab completion for the
following situations:

 - Completing the $INPUT record
 - File Completion
 - Completing Scale Parameters

*** General Tab Completion
*** File Completion
When completing a known file, Emacs Speaks NONMEM allows you to browse for a
file.  Under windows, there is an option to have a dialog to browse for the
file. This only occurs when there is no information about the file.  For
example:

#+BEGIN_SRC esn
$DATA
#+END_SRC

Pressing a tab after this will bring up file prompt.  This can be
toggled with:
*** Completing the $INPUT record
Another possibility is completing the =$INPUT= completely based on a dataset
specified.  This requires:

 - The input data to be a CSV file with a header
 - The data file to be already specified in the control stream

Therefore, for completion of a dataset, say =data.csv=, the by typing
the following,

#+BEGIN_SRC esn
$DATA data.csv
$INPUT
#+END_SRC

And Pressing a tab, gives:
#+BEGIN_SRC esn
;| Variables 10/50 |;
$INPUT C PAT TIME=TSFD AMT EVID SS II DV ID BLQ
;C ----------------------------------------------------------------------------
;C                                INPUT Variables
;C ----------------------------------------------------------------------------
;C C:     C
;C PAT:   PAT
;C TIME:  Time of Event (hr)
;C AMT:   Amount of Dose
;C EVID:  Event ID (0: Obs., 1: dose, 2: other, 3: reset, 4: reset & dose)
;C SS:    Steady State (0: Not steady-state, 1: steady-state, 2:steady-state superpositioned)
;C II:    Interdose Interval
;C DV:    Dependent Variable
;C ID:    NONMEM ID
;C BLQ:   BLQ
;C ----------------------------------------------------------------------------
$DATA data.csv IGNORE=C
#+END_SRC

After pressing tab:
  - Fills in the $INPUT record
    - Some variables are translated, such as TSFD, based on the user options
    - If there are too many variables, Emacs Speaks NONMEM will automatically
      drop/skip records.
  - Optionally places the =$DATA= record after the =$INPUT= record
    - Useful for NONMEM 6+'s =ACCEPT= and =IGNORE= statements such as
      =IGNORE==(=ID.EQ.2=)
  - Puts the =IGNORE==C option on the =$DATA= record.  This =C= is the first character
    of the header, and will be used to ignore the header any anything else that
    begins with =C=.
  - Optionally adds comments about the variables within the dataset
    - Used in EsN's Xpose script to add labels to the Xpose object to have more
      customizable plots.
  - Optionally moves the cursor to the =$DATA= record (may not move it if there
    are variables that should be dropped.


Overall, tab completion of the =$INPUT= record may be turned
on or off by:
 - Automation
   - Changes While Editing
     - Tab Completion Options
       - Use ESN's tab completion to fill in INPUT with CSV files


The moving of the =$DATA= record before the =$INPUT= record can be toggled by:

 - Automation
   - Changes While Editing
     - Tab Completion Options
       - After INPUT is tab completed, move $DATA before $INPUT and go to end of
         $DATA


The variable comments that can be used by the Xpose script can be toggled by:


 - Automation
   - Changes While Editing
     - Tab Completion Options
       - When completing $INPUT, comment about variable translation


The automatic variable labels may be changed/modified by the following
menu/customization:



 - Automation
   - Changes While Editing
     - Tab Completion Options
       - Customize the comments for of the input variable translation



The variable translation (i.e. DV=CP) for DV can be customized at:

 - Automation
   - Changes While Editing
     - Tab Completion Options
       - Customize assumed DV variable


The variable translation (i.e. TIME=TSFD) for TIME can be customized at:

 - Automation
   - Changes While Editing
     - Tab Completion Options
       - Customize assumed Time variable


Also when there are too many variables some variables are always kept.  The list
of these variables can be customized by:

 - Automation
   - Changes While Editing
     - Tab Completion Options
       - Customize assumed variables that are kept by default
         

*** Completing the Scaling parameter
Another feature is tab-completion of the scale parameter.  When the scale
parameter is not filled in, Emacs Speaks NONMEM prompts for the unit assumptions
(sometimes defaults are detected from your control stream), and then calculates
the scale factor.  For example,

#+BEGIN_SRC esn
$PK
  S1 =
#+END_SRC

After following the prompts, Emacs speaks NONMEM fills in the scale
parameter and the assumptions:

#+BEGIN_SRC esn
$PK
  S1 = V1/1000 ; Dose: mg; Volume: L; Conc: ng/mL
#+END_SRC

Note the scale factor calculation currently only works with oral
doses.  Support for some other dosage forms may be added in the
future.
*** Completing the FIXED component
** Seed Generation

When typing in the =$SIMULATION= block, often you have to choose a seed.  Emacs
Speaks NONMEM can choose this seed for you if you wish it to.  It chooses the
seed based on a uniform distribution of all numbers between 1 and 2,147,483,647,
the possible values of the =$SIMULATION= seed.  All this occurs if you press the
"=(=" key.  Therefore,

#+BEGIN_SRC esn
$SIMULATE
#+END_SRC

by just pressing the =(= key becomes:

#+BEGIN_SRC esn
$SIMULATE (1584308708
#+END_SRC

or some other random number between 1 and 2147483647.  This can be toggled at:

 - Automation
   - Changes While Editing
     - Generate Seed with "(" in $SIM record


** Numbering of estimates
Emacs Speaks NONMEM can automatically number the estimates of the =$THETA=,
=$OMEGA=, and =$SIGMA= blocks.  This is done automatically by pressing the
semi-colon.  For example:

#+BEGIN_SRC esn
$THETA (0 1)
#+END_SRC

By pressing the semi-colon can give the following (depending on the options
enabled):

#+BEGIN_SRC esn
$THETA (0 1) ; 1 -
#+END_SRC
or
#+BEGIN_SRC esn
$THETA (0 1) ;C 1 -
#+END_SRC
or
#+BEGIN_SRC esn
$THETA (0 1); THETA(1) -
#+END_SRC
or
#+BEGIN_SRC esn
$THETA (0 1) ;C THETA(1) -
#+END_SRC
These numbers update automatically.  Therefore,

#+BEGIN_SRC esn
$THETA (0 1) ; 1 -
       (0 2) ; 2 -

       (0 3) ; 3 -
       (0 4) ; 4 -
#+END_SRC

by typing an estimate between two and three will renumber the estimates
accordingly:

#+BEGIN_SRC esn
$THETA (0 1) ; 1 -
       (0 2) ; 2 -
       (0 100)
       (0 3) ; 4 -
       (0 4) ; 5 -
#+END_SRC

Also note that this works on =$OMEGA= and =$SIGMA= records with block statements.
Therefore,

#+BEGIN_SRC esn
$OMEGA
 1 ; 1 - One
 2 ; 2 - Two
 3 ; 3 - Three
 4 ; 4 - Four
 5 ; 5 - Five
 6 ; 6 - Six
#+END_SRC

would automatically be renumbered when adding a =BLOCK= statement to the beginning
of the  =$OMEGA= record:

#+BEGIN_SRC esn
$OMEGA BLOCK(3)
 1 ; 1 - One
 2 ; Two
 3 ; 2 - Three
 4 ; Four
 5 ; Five
 6 ; 3 - Six
#+END_SRC

This is a option that depends on the type of control stream you are
editing; one that is intended for census, PDx Pop, PLT tools, Pirana
or all other control streams have different options.  Therefore, by
default census and Pirana do not update numbers, but PDx Pop, PLT
tools, and all other control streams update numbers.

To toggle numbering of estimates for the type of control stream that you are
currently editing, this may be done at:

  - Automation
   - Changes While Editing
     - Number Estimates


To change it in the various modes you must find the option under


  - Automation
   - Changes While Editing
     - Program Specific Options

This is true of all other options discussed for estimate numbering. The other
options for estimate numbering are for using full labels (i.e. "=THETA(1) -="
instead of "=1 -="), and putting a =C= after the comment line (useful for
PLT tools).  The full variable label may be toggled at:


  - Automation
   - Changes While Editing
     - Options
       - Use full labels for numbering (i.e. instead of "1 -", use
         "THETA(1) -") *



The extra =C= may be toggled at:


  - Automation
   - Changes While Editing
     - Options
       - Use ;C for numbered comments (i.e. ";C 1 -" or ";C THETA(1) -") *

         
** Updating Block statements
Emacs Speaks NONMEM also numbers =$OMEGA=, and =$SIGMA=, block statements
automatically.  This is required for proper numbering, and updating
covariance labels.  Currently there is no toggle for this option, though it may
be missing if alignment, estimate numbering and covariance labels are all turned
off (haven't tested).

When the following =$OMEGA= block exists:

#+BEGIN_SRC esn
$OMEGA BLOCK(1)
 1 ; 1 -
 2 ;
#+END_SRC

Adding an estimate will automatically update the =BLOCK= number:

#+BEGIN_SRC esn
$OMEGA BLOCK(2)
 1 ; 1 -
 2 ;
 3
#+END_SRC

** Updating ADVAN/TRANS descriptions

Emacs Speaks NONMEM can automatically number the label the ADVAN/TRANS
descriptions.  For example:

#+BEGIN_SRC esn
;| Two Compartment Linear Mammillary Model w/First Order Absorpt. |;
;| In terms of CL, V, Q, VSS, and KA                              |;
$SUBROUTINES ADVAN4 TRANS3
#+END_SRC

becomes

#+BEGIN_SRC esn
;| Two Compartment Linear Mammillary Model                        |;
;| In terms of CL, V, Q, and VSS                                  |;
$SUBROUTINES ADVAN3 TRANS3
#+END_SRC

when changing =ADVAN4= to =ADVAN3=.  There are also larger descriptions of
ADVAN/TRANS combination that may be used.  For example, with large descriptions,
this becomes:

#+BEGIN_SRC esn
;| -------------------------------------------------------------- |;
;| Two Compartment Linear Mammillary Model (ADVAN3)               |;
;| -------------------------------------------------------------- |;
;|                                                                |;
;| +---+---------+-----+-----+-----+-----+-----+                  |;
;| | # | CMPTNME | INI | AOn | ADs | DDs | DOb |                  |;
;| +---+---------+-----+-----+-----+-----+-----+    |             |;
;| | 1 | Central | On. | No. | Yes | Yes | Yes |  +-+-+   +-+-+   |;
;| +---+---------+-----+-----+-----+-----+-----+  | 1 |<=>| 2 |   |;
;| | 2 | Periph. | On. | No. | Yes | No. | No. |  +-+-+   +-+-+   |;
;| +---+---------+-----+-----+-----+-----+-----+    |             |;
;| | 3 | Output. | Off | Yes | No. | No. | No. |    V             |;
;| +---+---------+-----+-----+-----+-----+-----+    3             |;
;|                                                                |;
;|                                                                |;
;| #         = Compartment Number                                 |;
;| CMPTNAME  = Compartment Name                                   |;
;| INI       = Initial Status                                     |;
;| AOn       = Allowed On/Off                                     |;
;| ADs       = Dose Allowed                                       |;
;| DDs       = Default for Dose                                   |;
;| DOb       = Default for Observation                            |;
;|                                                                |;
;| -------------------------------------------------------------- |;
;|                                                                |;
;| Shared PK parameters:                                          |;
;|                                                                |;
;|   S1         - Scale for central compartment (also called SC)  |;
;|   S2         - Scale for peripheral compartment                |;
;|   S3         - Scale for output compartment (also called S0)   |;
;|   F1         - Bioavailability for central compartment         |;
;|   F2         - Bioavailability for peripheral compartment      |;
;|   R1         - Rate for central compartment                    |;
;|   R2         - Rate for peripheral compartment                 |;
;|   D1         - Duration for central compartment                |;
;|   D2         - Duration for peripheral compartment             |;
;|   ALAG1      - Absorption lag for central compartment          |;
;|   ALAG2      - Absorption lag for peripheral compartment       |;
;|   F0         - Output fraction (also called F3, FO)            |;
;|   XSCALE     - X parameter                                     |;
;|                                                                |;
;| -------------------------------------------------------------- |;
;| TRANS3                                                         |;
;| -------------------------------------------------------------- |;
;|                                                                |;
;| CL   = Clearance                                               |;
;| V    = Central Volume                                          |;
;| Q    = Inter-compartmental clearance                           |;
;| VSS  = Volume of distribution at steady-state                  |;
;|                                                                |;
;| Relationships:                                                 |;
;|                                                                |;
;| K    = CL/V                                                    |;
;| K12  = Q/V                                                     |;
;| K21  = Q/(Vss-V)                                               |;
;|                                                                |;
;| -------------------------------------------------------------- |;
$SUBROUTINES ADVAN3 TRANS3
#+END_SRC
* Extended Control Stream Support
Extended control streams are originally defined in Nick Holford's
[[http://wfn.sourceforge.net/wfncs.htm#control_streams][Wings for NONMEM]] application]  Emacs for NONMEM allows extended
control streams to be used regardless of the platform you are using.
There are a few differences in implementation, though.  Much of this
description was shamelessly lifted (with permission) from the
[[http://wfn.sourceforge.net/wfncs.htm#control_streams][Wings for NONMEM extended control stream definitions]].
** Key Definitions
*** Parameter Names
Any control stream can be used with Emacs Speaks NONMEM. However,
certain conventions can be used to interface with Wings for NONMEM,
and allow renumbering of =THETA=, =OMEGA= and =SIGMA= variables within Emacs
Speaks NONMEM.  One of these conventions is formatting =$THETA=, =$OMEGA=
and =$SIGMA= records with one parameter value per line and adding a
comment on that line that contains a meaningful parameter name labels
the parameter.

Parameter names are taken from the first non-numeric word after the
comment delimiter =";"= e.g. the following examples all produce the
parameter name ="POPE0"= for =THETA(1)=.  Unlike a standard control stream
there is not a 6 character limit on the length of these names when
they are used on the right hand side of an expression.

#+BEGIN_SRC esn
$THETA (0,150.,) ; POPE0
$THETA (0,150.,) ; 1 POPE0
$THETA (0,150.,) ; POPE0 1
$THETA (0,150.,) ; pope0
$THETA (0,150.,) ; POPE0 L/min
#+END_SRC


Parameter names followed immediately by ="("= or ="{"= or ="["= or =";"= are
recognized e.g. all of the following will find "POPE0" as the
parameter name.

#+BEGIN_SRC esn
$THETA (0,150 ) ;POPE0(1)
$THETA (0,150 ) ;POPE0{1}
$THETA (0,150 ) ;POPE0[1]
$THETA (0,150 ) ;POPE0;1
#+END_SRC

When =$OMEGA BLOCKs= are used each row of off-diagonal and diagonal
elements should be on one line with a following comment and parameter
name for the diagonal element.

#+BEGIN_SRC esn
$OMEGA BLOCK (2)
0.5 ; ETAE0
0.1 0.5 ; ETAEMAX
#+END_SRC

If the SAME option is used then a parameter name can be specified by
inserting an $OMEGA BLOCK record with subsequent records with a ";;"
delimiter as the first two characters on the line. This convention is
only required when using an extended control stream.

#+BEGIN_SRC esn
$OMEGA BLOCK (2)
0.5 ; ETAE0
0.1 0.5 ; ETAEMAX
$OMEGA BLOCK (2) SAME
;; ETAE02
;; ETAEMAX2
#+END_SRC


** Extended Control Stream
Wings for NONMEM and Emacs Speaks NONMEM supports an extended form of NM-TRAN
control stream. The extended control stream is not case sensitive (but preserves
case of filenames, comments, exclusion characters, etc.), allows embedded tab
characters and does not require explicit =THETA=, =ETA= or =ERR/EPS= references in the
model code. Standard control stream records may be freely mixed with extended
control stream records when submiting to Wings for NONMEM.  Otherwise, Emacs
Speaks NONMEM chooses one model to maintain.
*** Example: Standard Control Stream

=Theopd.ctl= is an example of a standard NM-TRAN control stream which uses an Emax
pharmacodynamic model to describe the relationship between theophylline
concentration and peak expiratory flow rate (PEFR) in patients with severe
airways obstruction (Holford et al. 1993a).

#+BEGIN_SRC esn
$PROB theophylline pharmacodynamics standard control stream
$DATA theopd.dat IGNORE #
$INPUT ID TIME THEO AGE WT GEND RACE DIAG PEFR=DV
$ESTIM PRINT=1 POSTHOC
MSFO=theopd.msf
$COV
$THETA (0,150.,) ;popE0 1
$THETA (0,200.,) ;popEMAX 2
$THETA (.001,10,) ;popEC50 3
$OMEGA 0.5 ;etaE0 1
$OMEGA 0.5 ;etaEMAX 2
$OMEGA 0.5 ;etaEC50 3
$SIGMA 100 ;errSD 1
$PRED
E0=THETA(1)*EXP(ETA(1))
EMAX=THETA(2)*EXP(ETA(2))
EC50=THETA(3)*EXP(ETA(3))
Y = E0 + EMAX*THEO/(THEO+EC50) + ERR(1)
$TABLE ID TIME THEO AGE WT GEND RACE DIAG
E0 EMAX EC50 Y
NOPRINT ONEHEADER FILE=theopd.fit
#+END_SRC





*** Example: Extended Control Stream
The following is an example of an extended control stream (theopd2.ctl) which
implements the same code as the standard control stream. Parameter names are
used in the model records to identify where =THETA=, =ETA= and =ERR= references are
required when the extended control stream is translated into the standard
control stream for NM-TRAN. Use of the extended control stream means it is no
longer necessary to count each of the =$THETA=, =$OMEGA= and =$SIGMA= records and
tediously ensure they match with the =THETA=, =ETA= and =ERR= subscripts in the model.

I have used the prefix ="pop"= for population parameters, ="eta"= for population
parameter random effects and ="err"= or ="eps"= for residual unknown random
effects. This is a personal convention. You may choose any parameter names you
wish to identify =THETA=, =ETA= and =ERR/EPS= in the control stream.

#+BEGIN_SRC esn
$PROB theophylline pharmacodynamics extended control stream
$DATA theopd.dat IGNORE #
$INPUT ID TIME THEO AGE WT GEND RACE DIAG PEFR=DV
$ESTIM PRINT=1 POSTHOC
MSFO=theopd.msf
$COV
$THETA (0,150.,) ; popE0
$THETA (0,200.,) ; popEMAX
$THETA (.001,10,) ; popEC50
$OMEGA 0.5 ; etaE0
$OMEGA 0.5 ; etaEMAX
$OMEGA 0.5 ; etaEC50
$SIGMA 100 ; errSD
$PRED
E0=pope0*EXP(etae0)
EMAX=popemax*EXP(etaemax)
EC50=popec50*EXP(etaec50)
Y = E0 + EMAX*THEO/(THEO+EC50) + errsd
$TABLE ID TIME THEO AGE WT GEND RACE DIAG
E0 EMAX EC50 Y
NOPRINT ONEHEADER FILE=theopd.fit
#+END_SRC

A special convention is used when parameter names occur on the left hand side of
an expression. Once a parameter name has appeared on the left hand side no
further substitutions are made when that parameter name appears in any following
right hand side expressions. A model record in this context is an =$PRED=, =$PK=,
=$ERROR=, =$DES=, =$AES= record. After translation the extended control stream example
will be identical to the standard control stream example.

** Using ESN's extended control streams
There are two options for using ESN's extended control streams:

 - Use it manually and/or in conjunction with Wings for NONMEM
 - Automatically translate between a standard control stream and extended
   control stream.

When using extended control stream options manually, you may change back and
forth between =THETA, ETA,= and =ERR/EPS= values through the following two menu
options:

 - Extended
   - Change THETA, ETA,EPS to WFN parameter names

or

 - Tools
   - Change THETA, ETA,EPS to WFN parameter names

The other option is to automate extended control stream translation.  This opens
files with the extended control stream parameter names filled in, and then saves
as a standard control stream.  Unlike WFN, mixing extended control stream
variable names and standard =THETA(1)=, =ETA(1)=, =EPS(1)= is not well supported.
While you may enter =THETA(1)= into the control stream, upon save, this is change
to the label for =THETA(1)=, like =popE0=.  Automating extended control stream may
be toggled with:

- Extended
   - Translate Extended Control Streams to Normal Control Streams\; Open
       Extended


** Emacs Speaks NONMEM differences/options
*** Mu variable referencing (not implemented in WFN)
NONMEM 7 introduces =MU= variable referencing ([[mu][described here]]).  Each =MU_i=
variable mus be associated with an =ETA=, and each =MU_i= must have a relationship
that is of the form =MU_i+ETA(i)=.  For this reason, an additional translation has
been defined in the extended control stream when using EsN, that is:
 - =MU_eta_variable_label=  is translated to =MU_i=
 - =MU+eta_variable_label= is translated to =MU_i+ETA(i)=.

For example, if the following =$OMEGA= block is defined,

#+BEGIN_SRC esn
$OMEGA 0.5 ; etaE0
$OMEGA 0.5 ; etaEMAX
$OMEGA 0.5 ; etaEC50
</src>

The following table details the translations that may occur:

Original Value || Extended Control Stream
MU_1 = log(THETA(1))  | MU_etaE0 = log(THETA(1))
E0 = EXP(MU_1+ETA(1)) | E0 = EXP(MU_etaE0+etaE0)
E0 = EXP(MU_1+ETA(1)) | E0 = EXP(MU+etaE0)
E0 = EXP(ETA(1)+MU_1) | E0 = EXP(etaE0+MU_etaE0)
E0 = EXP(ETA(1)+MU_1) | E0 = EXP(etaE0+MU)


A full example of an extended control stream is:

#+BEGIN_SRC esn
$PROB theophylline pharmacodynamics extended control stream
$DATA theopd.dat IGNORE #
$INPUT ID TIME THEO AGE WT GEND RACE DIAG PEFR=DV
$ESTIM PRINT=1 POSTHOC
MSFO=theopd.msf
$COV
$THETA (0,150.,) ; popE0
$THETA (0,200.,) ; popEMAX
$THETA (.001,10,) ; popEC50
$OMEGA 0.5 ; etaE0
$OMEGA 0.5 ; etaEMAX
$OMEGA 0.5 ; etaEC50
$SIGMA 100 ; errSD
$PRED
mu_etae0 = log(pope0)
mu_etaemax = log(popemax)
mu_etaec50 = log(popec50)
E0=EXP(mu+etae0)
EMAX=EXP(mu+etaemax)
EC50=EXP(mu+etaec50)
Y = E0 + EMAX*THEO/(THEO+EC50) + errsd
$TABLE ID TIME THEO AGE WT GEND RACE DIAG
E0 EMAX EC50 Y
NOPRINT ONEHEADER FILE=theopd.fit
#+END_SRC

This is equivalent to a standard control stream:

#+BEGIN_SRC esn
$PROB theophylline pharmacodynamics extended control stream
$DATA theopd.dat IGNORE #
$INPUT ID TIME THEO AGE WT GEND RACE DIAG PEFR=DV
$ESTIM PRINT=1 POSTHOC
MSFO=theopd.msf
$COV
$THETA (0,150.,) ; popE0
$THETA (0,200.,) ; popEMAX
$THETA (.001,10,) ; popEC50
$OMEGA 0.5 ; etaE0
$OMEGA 0.5 ; etaEMAX
$OMEGA 0.5 ; etaEC50
$SIGMA 100 ; errSD
$PRED
mu_1 = log(THETA(1))
mu_2 = log(THETA(2))
mu_3 = log(THETA(3))
E0= EXP(mu_1+ETA(1))
EMAX= EXP(mu_2+ETA(2))
EC50= EXP(mu_3+ETA(3))
Y = E0 + EMAX*THEO/(THEO+EC50) + EPS(1)
$TABLE ID TIME THEO AGE WT GEND RACE DIAG
E0 EMAX EC50 Y
NOPRINT ONEHEADER FILE=theopd.fit
#+END_SRC

*** Optional Skipping of variable names
When using [[plt][PLT tools]], you may wish to add comments that a
read in the brief summary pdf file that will include a description of
the parameter estimated.  According to PLT tools' manual, any comment
that is coded with =;C= will be included in this file.  The following
code would be recognized by PLT tools:

#+BEGIN_SRC esn
$THETA (0,150.,)  ;C THETA(1) - popE0
$THETA (0,200.,)  ;C THETA(2) - popEMAX
$THETA (.001,10,) ;C THETA(3) - popEC50
$OMEGA 0.5 ;C ETA(1) - etaE0
$OMEGA 0.5 ;C ETA(2) - etaEMAX
$OMEGA 0.5 ;C ETA(3) - etaEC50
$SIGMA 100 ;C EPS(1) - errSD
#+END_SRC

After being recognized, the following text will be added to the brief summary:

: THETA(1) - popE0
: THETA(2) - popEMAX
: THETA(3) - popEC50
: ETA(1) - etaE0
: ETA(2) - etaECMAX
: ETA(3) - etaEC50
: EPS(1) - errSD

which may be more clear than other types of labeling.  According to
Wings-for-NONMEM, these would all would be invalid labels since they
are all =C= variable labels.  In Emacs Speaks Statistcs, however, this
initial =C THETA=, =C ETA=, or =C EPS= can be ignored, and the variables would
still have the names =popE0=, =popEMAX=, etc.

With this in mind, the following control stream would still be valid:

#+BEGIN_SRC esn
$PROB theophylline pharmacodynamics extended control stream
$DATA theopd.dat IGNORE #
$INPUT ID TIME THEO AGE WT GEND RACE DIAG PEFR=DV
$ESTIM PRINT=1 POSTHOC
MSFO=theopd.msf
$COV
$THETA (0,150.,)  ;C THETA(1) - popE0
$THETA (0,200.,)  ;C THETA2 - popEMAX
$THETA (.001,10,) ;C THETA[3] - popEC50
$OMEGA 0.5 ;C ETA(1) - etaE0
$OMEGA 0.5 ;  ETA(2) - etaEMAX
$OMEGA 0.5 ;C OMEGA3 -  etaEC50
$SIGMA 100 ;C SIGMA1 errSD
$PRED
E0=pope0*EXP(etae0)
EMAX=popemax*EXP(etaemax)
EC50=popec50*EXP(etaec50)
Y = E0 + EMAX*THEO/(THEO+EC50) + errsd
$TABLE ID TIME THEO AGE WT GEND RACE DIAG
E0 EMAX EC50 Y
NOPRINT ONEHEADER FILE=theopd.fit
#+END_SRC

Overall, Emacs Speaks NONMEM would ignore:
 - Any C's after the initial comment
 - =THETA=
 - =THETA#=
 - =THETA(#)=
 - =THETA[#]=
 - =THETA{#}=
 - =ETA=
 - =ETA#=
 - =ETA(#)=
 - =ETA[#]=
 - =ETA{#}=
 - =OMEGA=
 - =OMEGA#=
 - =OMEGA(#)=
 - =OMEGA[#]=
 - =OMEGA{#}=
 - =EPS=
 - =EPS#=
 - =EPS(#)=
 - =EPS[#]=
 - =EPS{#}=
 - =ERR=
 - =ERR#=
 - =ERR(#)=
 - =ERR[#]=
 - =ERR{#}=
 - =SIGMA=
 - =SIGMA#=
 - =SIGMA(#)=
 - =SIGMA[#]=
 - =SIGMA{#}=

Where =#= represents an integer.


*** Parameter Names in the ZERO option
In addition to specifying variables in the =$PK= or =$PRED= portion of the control
stream, Emacs Speaks NONMEM allows variable labels in the =ZERO== option of the
=$ESTIMATION= record,  For example:

#+BEGIN_SRC esn
$PROB theophylline pharmacodynamics extended control stream
$DATA theopd.dat IGNORE #
$INPUT ID TIME THEO AGE WT GEND RACE DIAG PEFR=DV
$ESTIM PRINT=1 METHOD=HYBRID ZERO=(etaE0)
MSFO=theopd.msf
$COV
$THETA (0,150.,) ; popE0
$THETA (0,200.,) ; popEMAX
$THETA (.001,10,) ; popEC50
$OMEGA 0.5 ; etaE0
$OMEGA 0.5 ; etaEMAX
$OMEGA 0.5 ; etaEC50
$SIGMA 100 ; errSD
$PRED
E0=pope0*EXP(etae0)
EMAX=popemax*EXP(etaemax)
EC50=popec50*EXP(etaec50)
Y = E0 + EMAX*THEO/(THEO+EC50) + errsd
$TABLE ID TIME THEO AGE WT GEND RACE DIAG
E0 EMAX EC50 Y
NOPRINT ONEHEADER FILE=theopd.fit
#+END_SRC

On save, the =$ESTIM= record is translated to:

#+BEGIN_SRC esn
$ESTIM PRINT=1 METHOD=HYBRID ZERO=(1)
#+END_SRC

This is only occurs when Emacs Speaks NONMEM handles Extended Control stream
translation is enabled.

This feature may also be toggled at:

 - Extended
   - Translate extended parameter names names in ZERO=(x)

** Parameter Names that are not allowed
Emacs Speaks NONMEM does not allow certain reserved keywords to be variable
labels.  For example =EXP= and =DEXP= are reserved in NONMEM control streams to have
a specific meaning, and cannot be assigned to a variable label.

** Changing Parameter Names
If you wish to change the =popE0= parameter name to =E0=, editing the label causes
*all* of the parameter names referenced in the document to change without any
extra effort.  This option may be toggled at:

- Extended
   - Update variable names in model blocks when updating labels in estimates

*Currently Causes Emacs to Crash*

** Parameter Name Highlighting

When this option is enabled, parameter names are highlighted in the
control-stream, making them easier to identify.

This may be toggled at:

 - Extended
   - Highlight Extended Control Stream Parameter Names

** Lower case preference

When opening a normal control stream as an extended control stream, there is an
option to have the records in lower case instead of upper case;  This may be
toggled at:

 - Extended
   - When opening extended, prefer records in lower case


* Hacking Emacs Speaks NONMEM
** Adding customized tables
You may add customized tables that will be generated by using =esn-deftable=
** Hooks
- =esn-exit-record-hook= :: Hook to run when exiting a record
- =(esn-tos-post-hook 'hook)= :: Add 'hook to =$THETA= =$OMEGA= and
     =$SIGMA= blocks.


- esn-enter-record-hook
- esn-exit-record-hook

esn-rec-pre-command-hook
esn-rec-post-command-hook
esn-rec-modification-hook
